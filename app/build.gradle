plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
    id 'com.google.dagger.hilt.android'
    id 'com.google.firebase.crashlytics'
}

def releaseKeystorePropFile = rootProject.file("signing/release.properties")

def debugKeystorePropFile = rootProject.file("signing/debug.properties")
def debugKeystoreProp = new Properties()
debugKeystoreProp.load(new FileInputStream(debugKeystorePropFile))

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.nszalas.timefulness"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile rootProject.file(debugKeystoreProp['storeFile'])
            storePassword debugKeystoreProp['storePassword']
            keyAlias debugKeystoreProp['keyAlias']
            keyPassword debugKeystoreProp['keyPassword']
        }
    }

    if (releaseKeystorePropFile.exists()) {
        Properties releaseKeystoreProp = new Properties()
        releaseKeystoreProp.load(new FileInputStream(releaseKeystorePropFile))

        signingConfigs {
            release {
                storeFile rootProject.file(releaseKeystoreProp['storeFile'])
                storePassword releaseKeystoreProp['storePassword']
                keyAlias releaseKeystoreProp['keyAlias']
                keyPassword releaseKeystoreProp['keyPassword']
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            resValue "string", "app_name", "Timefulness-debug"
            minifyEnabled false
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Timefulness"
            minifyEnabled true
            shrinkResources true
        }

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def baseName = ""
                if(output.baseName == "debug") { baseName = "-${output.baseName}" }

                def newName = "Timefulness${baseName} ${variant.versionName}.apk"
                output.outputFileName = newName
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // hilt
    implementation 'com.google.dagger:hilt-android:2.44.2'
    kapt 'com.google.dagger:hilt-compiler:2.44.2'

    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'

    // navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'

    // firebase
    implementation platform('com.google.firebase:firebase-bom:31.1.1')
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    implementation 'com.google.android.gms:play-services-auth:20.4.0'

    // bar chart
    implementation project(path: ':simpleandroidbarchart')

    // tests
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // room
    implementation "androidx.room:room-ktx:2.4.3"
    implementation "androidx.room:room-runtime:2.4.3"
    kapt "androidx.room:room-compiler:2.4.3"

    // glide
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.14.2'

    // calendar
    implementation 'com.github.SimpleMobileTools:Simple-Commons:f538687d6e'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
}

kapt {
    correctErrorTypes = true
}